def String[] modules = [
    'core',
    'web',
    'ldap',
    'acl',
    'config',
    'cas',
    'openid',
    'taglibs',
    'aspects'
]

def String[] samples = [
    'tutorial',
    'contacts',
    'openid'
]

def String[] docs = [
    'faq',
    'manual'
]

def String[] itest = [
    'web'
]

include modules

modules.each {name ->
    p = findProject(":${name}")
    p.name = "spring-security-${name}"
    p.buildFileName = "${name}.gradle"
}

include samples

samples.each {name ->
    p = findProject(":${name}")
    p.name = "spring-security-samples-${name}"
    p.buildFileName = "${name}.gradle"
    p.projectDir = new File(settingsDir, "samples/${name}");
}

include itest

itest.each { name ->
    p = findProject(":${name}")
    p.name = "itest-${name}"
    p.buildFileName = "itest-${name}.gradle"
    p.projectDir = new File(settingsDir, "itest/${name}");
}

include docs

docs.each { name ->
    p = findProject(":${name}")
    p.buildFileName = "${name}.gradle"
    p.projectDir = new File(settingsDir, "docs/${name}");
}

rootProject.children.each {project ->
    assert project.projectDir.isDirectory()
    assert project.buildFile.isFile()
}