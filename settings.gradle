def String[] modules = [
    'core',
    'web',
    'ldap',
    'acl',
    'config',
    'cas',
    'openid',
    'taglibs'
]

def String[] samples = [
    'tutorial',
    'contacts',
    'openid'
]

def String[] docs = [
    'faq',
    'manual'
]

include modules

modules.each {name ->
    p = findProject(":${name}")
    p.name = "spring-security-${name}"
    p.buildFileName = "${name}.gradle"
}

include samples

samples.each {name ->
    p = findProject(":${name}")
    p.name = "spring-security-samples-${name}"
    p.buildFileName = "${name}.gradle"
    p.projectDir = new File(settingsDir, "samples/${name}");
}

include docs

docs.each{ name ->
    p = findProject(":${name}")
    p.buildFileName = "${name}.gradle"
    p.projectDir = new File(settingsDir, "docs/${name}");
}

rootProject.children.each {project ->
    assert project.buildFile.isFile()
    assert project.projectDir.isDirectory()
}