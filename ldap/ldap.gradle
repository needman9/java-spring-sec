// Ldap build file

apacheds_libs = [
         "org.apache.directory.server:apacheds-core:$apacheDsVersion",
         "org.apache.directory.server:apacheds-core-entry:$apacheDsVersion",
         "org.apache.directory.server:apacheds-protocol-shared:$apacheDsVersion",
         "org.apache.directory.server:apacheds-protocol-ldap:$apacheDsVersion",
         "org.apache.directory.server:apacheds-server-jndi:$apacheDsVersion",
         'org.apache.directory.shared:shared-ldap:0.9.15'
]

dependencies {
    compile project(':spring-security-core'),
            "org.springframework:spring-beans:$springVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-tx:$springVersion",
            'ldapsdk:ldapsdk:4.1'
    apacheds_libs.collect {
        compile (it) {
            exclude group: 'org.slf4j'
        }
    }

    compile ("org.springframework.ldap:spring-ldap-core:$springLdapVersion") {
        exclude(group: 'commons-logging', module: 'commons-logging')
        exclude(group: 'org.springframework', module: 'spring-core')
        exclude(group: 'org.springframework', module: 'spring-tx')
        exclude(group: 'org.springframework', module: 'spring-beans')
    }
}

integrationTest {
    include('**/ApacheDSServerIntegrationTests.class')
//    exclude('**/OpenLDAPIntegrationTestSuite.class')
    maxParallelForks = 1
    systemProperties['apacheDSWorkDir'] = "${buildDir}/apacheDSWork"
}

task(ldapServer, dependsOn: 'integrationTestClasses', type: JavaExec) {
    classpath = sourceSets.integrationTest.runtimeClasspath
    main = 'org.springframework.security.ldap.ApacheDSServerIntegrationTests'
    systemProperties['apacheDSWorkDir'] = "${buildDir}/apacheDSWork"
}

