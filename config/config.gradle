// Config Module build file

apply plugin: 'groovy'

compileTestJava.dependsOn(':spring-security-core:compileTestJava')

configurations {
    // GRADLE-1124
    compile.extendsFrom = []
    testCompile.extendsFrom groovy
}

dependencies {
    compile project(':spring-security-core'),
            project(':spring-security-web'),
            "org.aspectj:aspectjweaver:$aspectjVersion",
            'aopalliance:aopalliance:1.0',
            "org.springframework:spring-aop:$springVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-web:$springVersion",
            "org.springframework:spring-beans:$springVersion"

    provided "javax.servlet:servlet-api:2.5"

    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.4'

    testCompile project(':spring-security-ldap'),
                project(':spring-security-openid'),
                'org.openid4java:openid4java-nodeps:0.9.5',
                project(':spring-security-core').sourceSets.test.classes,
                'javax.annotation:jsr250-api:1.0',
                "org.springframework.ldap:spring-ldap-core:$springLdapVersion",
                "org.springframework:spring-jdbc:$springVersion",
                "org.springframework:spring-tx:$springVersion",
                'org.spockframework:spock-core:0.4-groovy-1.7',
                "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    testRuntime "hsqldb:hsqldb:$hsqlVersion",
                "cglib:cglib-nodep:2.2"
}

task ldapTests(type: Test) {
    jvmArgs = ['-ea', '-Xmx500m', "-DapacheDSWorkDir=${buildDir}/apacheDSWork"]
    include ("**/ldap/**")
    maxParallelForks = 1
   // GRADLE-1090
    testClassesDir = sourceSets.test.classesDir
    classpath = sourceSets.test.runtimeClasspath
    testReport = false
}

test {
    dependsOn ldapTests
    exclude ("**/ldap/**")
}
